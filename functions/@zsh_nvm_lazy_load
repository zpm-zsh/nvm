# Get all global node module binaries including node
# (only if NVM_NO_USE is off)
local global_binaries
if [[ "$NVM_NO_USE" == true ]]; then
  global_binaries=()
else
  global_binaries=($(@zsh_nvm_global_binaries))
fi

# Add yarn lazy loader if it's been installed by something other than npm
@zsh_nvm_has yarn && global_binaries+=('yarn')

# Add nvm
global_binaries+=('nvm')
global_binaries+=($NVM_LAZY_LOAD_EXTRA_COMMANDS)

# Remove any binaries that conflict with current aliases
local cmds
cmds=()
local bin
for bin in $global_binaries; do
  [[ "$(which $bin 2> /dev/null)" = "$bin: aliased to "* ]] || cmds+=($bin)
done

# Create function for each command
local cmd
for cmd in $cmds; do

  # When called, unset all lazy loaders, load nvm then run current command
  eval "$cmd(){
      unset -f $cmds > /dev/null 2>&1
      @zsh_nvm_load
      $cmd \"\$@\"
  }"
done
